plugins {
    id "java"
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = "dev.pelfox"
version = project.property("project.version")

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:${project.property("minecraft.version")}-R0.1-SNAPSHOT")
}

tasks {
    runServer {
        minecraftVersion(project.property("minecraft.version") as String)
        def jvmArgsEnv = System.getenv("PAPER_DEV_JVM_ARGS")
        if (jvmArgsEnv != null && !jvmArgsEnv.trim().isEmpty()) {
            jvmArgs = jvmArgsEnv.split("\\s+")
        }
    }
}

def targetJavaVersion = project.property("java.version") as Integer
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    filesMatching("plugin.yml") {
        expand([version: version])
    }
}
